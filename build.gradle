plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.12'
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'de.tum.ise'
version = '1.0.0'
sourceCompatibility = 17
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

configurations {
    serverImplementation.extendsFrom(implementation)
    clientImplementation.extendsFrom(implementation)
}

sourceSets {
    common {
        java {
            srcDir 'src/common/java'
        }
    }
    server {
        java {
            srcDir 'src/server/java'
        }
        resources {
            srcDir 'src/server/resources'
        }
        compileClasspath += common.output
        runtimeClasspath += common.output
    }
    client {
        java {
            srcDir 'src/client/java'
        }
        resources {
            srcDir 'src/client/resources'
        }
        compileClasspath += common.output
        runtimeClasspath += common.output
    }
}

dependencies {
    serverImplementation 'org.springframework.boot:spring-boot-starter-web'
    clientImplementation 'org.springframework.boot:spring-boot-starter-webflux'
}

javafx {
    version = '17.0.2'
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.media' ]
    configuration = 'clientImplementation'
}

application {
    mainClass = 'de.tum.ise.Starter'
}

run {
    group = "Application"
    description = "Runs the JavaFX client."
    classpath = sourceSets.client.runtimeClasspath
}

bootRun {
    group = "Application"
    description = "Runs the Spring Boot server."
    mainClass = 'de.tum.ise.T10E01ServerApplication'
    classpath = sourceSets.server.runtimeClasspath
}

processServerResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}